apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    ktlint
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // ktlint
    ktlint "com.github.shyiko:ktlint:0.31.0"

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // Testing
    testImplementation 'junit:junit:4.13'
    testImplementation 'io.mockk:mockk:1.10.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

task ktlint(type: JavaExec, group: "kotlin verification") {
    description = 'Check Kotlin code style.'
    args 'src/**/*.kt'
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
}
check.dependsOn ktlint

task ktFormat(type: JavaExec, group: "kotlin Formatting") {
    description = 'Fix Kotlin code style deviations.'
    args '-F', 'src/**/*.kt'
    main = 'com.github.shyiko.ktlint.Main'
    classpath = configurations.ktlint
}

bintray {
    user = localProperties.containsKey("bintrayUser") ? localProperties["bintrayUser"] : ""
    key = localProperties.containsKey("bintrayKey") ? localProperties["bintrayKey"] : ""
    publications = ['release', 'debug']
    pkg {
        repo = 'DevelopmentMode'
        name = 'fr.bowser.dev.development-mode'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bowserf/development-mode.git'
        version {
            name = rootProject.ext.versionName
            released  = new Date()
        }
    }
}

afterEvaluate {
    task generateJavadoc(type: Javadoc) {
        failOnError false
        source = android.sourceSets.main.java.sourceFiles
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.compile
    }

    task javadocJar(type: Jar, dependsOn: generateJavadoc) {
        classifier = 'javadoc'
        from generateJavadoc.destinationDir
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact sourcesJar
                artifact javadocJar

                // You can then customize attributes of the publication as shown below.
                groupId = rootProject.ext.mavenGroupId
                artifactId = rootProject.ext.mavenArtifactId
                version = rootProject.ext.versionName
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug
                artifact sourcesJar
                artifact javadocJar

                groupId = rootProject.ext.mavenGroupId
                artifactId = rootProject.ext.mavenArtifactIdDebug
                version = rootProject.ext.versionName
            }
        }
    }
}
